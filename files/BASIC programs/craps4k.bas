100 REM  NAME--CRAPS
110 REM
120 REM  DESCRIPTIONS--A SESSION AT THE CRAPS TABLE.
130 REM
140 REM  SOURCE--UNKNOWN
150 REM
160 REM  INSTRUCTIONS--TYPE "RUN" AND FOLLOW INSTRUCTIONS.
170 REM
180 REM
190 REM  *  *  *  *  *  *   MAIN PROGRAM  *  *  *  *  *  *  *  *  *  *
200 REM
210 REM  RANDOMIZE - COMMAND NOT IN 4K BASIC
220 PRINT
230 PRINT "WELCOME TO THE DIGITAL CRAPS TABLE."
240 PRINT "DO YOU WANT INSTRUCTIONS (0=NO  1=YES) ";
250 INPUT Z0
260 IF Z0=0 THEN 350
270 IF Z0=1 THEN 300
280 PRINT "INCORRECT ANSWER.  PLEASE TYPE '1' OR '0'";
290 GOTO 250
300  PRINT " ON THE FIRST THROW 2'S,3'S AND 12'S LOSE, 7'S AND 11'S"
310  PRINT " WIN.  ON LATER THROWS 7'S AND 11'S LOSE, THE ORIGINAL"
320  PRINT " NUMBER WINS, AND ANY OTHER RESULT CAUSES ANOTHER THROW."
330  PRINT " BETS MAY RANGE FROM .01 TO 10000, TYPE '-1' TO FIND THE"
340  PRINT " STATE OF YOUR WINNINGS OR LOSSES, AND A '0' TO QUIT."
350  PRINT
360  PRINT
370 PRINT
380 PRINT " HOW MUCH DO YOU BET";
390 INPUT B1
400 IF B1<0 THEN 840
410 IF B1=0 THEN 960
420 IF B1<5 THEN 940
430 IF B1>10000 THEN 820
440 LET T7=0
450 LET D1=INT(6*RND(1))+1
460 LET D2=INT(6*RND(1))+1
470 PRINT D1;D2;
480 LET T=D1+D2
490 IF T7>0 THEN 520
500 IF T=12 THEN 750
510 IF T=2 THEN 770
520 IF T=8 THEN 560
530 IF T=11 THEN 560
540 PRINT " A";T;
550 GOTO 570
560 PRINT " AN";T;
570 IF T7>0 THEN 630
580 IF T=3 THEN 780
590 IF T=7 THEN 710
600 IF T=11 THEN 710
610 LET D9=T
620 GOTO 680
630 IF T=7 THEN 780
640 IF T=11 THEN 780
650 IF T=D9 THEN 710
660 PRINT
670 GOTO 450
680 PRINT " YOUR NUMBER IS " T
690 LET T7=1
700 GOTO 450
710 PRINT " YOU WIN"
720 LET W=W+B1
730 PRINT " YOU WIN" B1;"DOLLARS"
740 GOTO 370
750 PRINT " BOXCARS.....";
760 GOTO 780
770 PRINT " SNAKES EYES.....";
780 LET W=W-B1
790 PRINT " YOU LOSE"
800 PRINT " YOU LOSE" B1; "DOLLARS"
810 GOTO 370
820 PRINT " ARE YOU KIDDING ME? TRY A LITTLE LESS"
830 GOTO 370
840 PRINT
850 IF W>=0 THEN 920
860 PRINT" YOU ARE BEHIND";-W;"DOLLARS--TRY SOME MORE (1=YES  0=NO) ";
870 INPUT H0
880 IF H0=0 THEN 960
890 IF H0=1 THEN 370
900 PRINT"INCORRECT ANSWER. PLEASE TYPE '1' OR '0'";
910 GOTO 870
920 PRINT " YOU ARE AHEAD"W;" DOLLARS--HOW ABOUT A LITTLE MORE";
930 GOTO 870
940 PRINT " YOU ARE A CHEAPSKATE--BUT I'LL PLAY"
950 GOTO 440
960 PRINT
970 PRINT
980 PRINT
990 PRINT
1000 IF W>=0 THEN 1040
1010 PRINT"  YOU LOST";-W;"DOLLARS./. TOUGH LIFE"
1020 PRINT " LET THIS BE A LESSON"
1030 STOP
1040 PRINT " YOU WON " W; "DOLLARS...WILL SEND CHECK IMMEDIATELY"
1050 END
