10 WIDTH 255
20 PRINT CHR$(27)+"c"
30 FOR X=1 TO 80
40 PRINT CHR$(27)+"[1;"+MID$(STR$(X),2)+"f";
50 PRINT"*";
60  PRINT CHR$(27)+"[24;"+MID$(STR$(X),2)+"f";
70 PRINT"*";
80 NEXT X
90 FOR X=1 TO 24
100  PRINT CHR$(27)+"["+MID$(STR$(X),2)+";1f";
110 PRINT"*";
120 PRINT CHR$(27)+"["+MID$(STR$(X),2)+";80f";
130 PRINT"*";
140 NEXT X
150 X=1:FLAG=1
160 PRINT CHR$(27)+"[1m"
170 PRINT CHR$(27)+"[23;2f"+"Hit SPACE key!"
180 PRINT CHR$(27)+"[0m"
190 A$="VT 100 test program V 1.0 Copyright 2003 by Joseph Forgione no rights re
served"
200 FOR Z=1 TO 30
210 X=X+1
220 IF X>LEN(A$) THEN X=1
230 N$=N$+MID$(A$,X,1)
240 NEXT Z
250 PRINT CHR$(27)+"[12;25f";
260 PRINT N$
270 PRINT CHR$(27)+"[23;2f";
280 IF FLAG=1 THEN PRINT CHR$(27)+"[2@";:COUNT = COUNT +1
290 IF FLAG=0 THEN  PRINT CHR$(27)+"[2P";:COUNT =COUNT -1
300 IF COUNT >30 THEN FLAG=0
310 IF COUNT <1 THEN FLAG=1
320 N$="":X=M:M=M+1
330 IF M> LEN(A$) THEN M=1
340 X$=INKEY$:IF X$="" THEN GOTO 200
350 PRINT CHR$(27)+"c"
360 PRINT CHR$(27)+"[1;25f";
370 PRINT"Testing CUB CUD CUF CUU HVP RIS"
380 PRINT CHR$(27)+"[12;40f";
390 FOR Y=1 TO 10
400 PRINT CHR$(27)+"[1A";
410 PRINT CHR$(27)+"[1D";
420 PRINT"x";
430 NEXT Y
440 PRINT CHR$(27)+"[12;40f";
450 FOR Y=1 TO 10
460 PRINT CHR$(27)+"[1B";
470 PRINT CHR$(27)+"[1D";
480 PRINT"x";
490 NEXT Y
500  PRINT CHR$(27)+"[12;40f";
510 FOR Y=1 TO 10
520 PRINT CHR$(27)+"[1B";
530 PRINT"y";
540 NEXT Y
550 PRINT CHR$(27)+"[12;40f";
560 FOR Y=1 TO 10
570 PRINT CHR$(27)+"[1C";
580 PRINT"z";
590 NEXT Y
600 PRINT CHR$(27)+"[23;30f";
610 PRINT"Hit SPACE."
620 X$=INKEY$:IF X$="" GOTO 620
630 PRINT CHR$(27)+"c";
640 PI=3.1415:XX=1
650 FOR X=0 TO PI*2 STEP (PI*2)/79
660 Y=SIN(X)
670 Y=(Y*10):Y=INT(Y):Y=Y+12
680 X$=MID$(STR$(XX),2):Y$=MID$(STR$(Y),2)
690 PRINT CHR$(27)+"["+Y$+";"+X$+"f";
700 PRINT"*";
710 XX=XX+1
720 NEXT X
730  PRINT CHR$(27)+"[23;30f";
740  PRINT"Hit SPACE."
750 X$=INKEY$:IF X$="" GOTO 750
760 X$=""
770 FOR A=1 TO 80:X$=X$+"*":NEXT A
780 PRINT CHR$(27)+"c";
790 FOR A=1 TO 24:PRINT X$:NEXT A
800 PRINT CHR$(27)+"[12;40f";
810 PRINT CHR$(27)+"[1J";
820 PRINT CHR$(27)+"[1;1f";
830 PRINT"Erase from start of screen to cursor"
840 PRINT CHR$(27)+"[1;1f";
850 FOR T=1 TO 1000*5:NEXT T
860 FOR A=1 TO 12:PRINT X$:NEXT A
870  PRINT CHR$(27)+"[12;40f";
880 PRINT CHR$(27)+"[0J";
890 PRINT CHR$(27)+"[24;1f";
900 PRINT"Erase Cursor to end of screen"
905 PRINT CHR$(27)+"[12;40f";
910 FOR T=1 TO 1000*5:NEXT T
911 FOR Y=1 TO 12:PRINT X$:NEXT Y
920 FOR Y=1 TO 24:Y$=MID$(STR$(Y),2)
930 PRINT CHR$(27)+"["+Y$+";40f";
940 PRINT CHR$(27)+"[0K";
950 FOR T=1 TO 200:NEXT T
960 PRINT CHR$(27)+"[1K";
970 FOR T=1 TO 100:NEXT T
980 NEXT Y
990 PRINT CHR$(27)+"c"
1000 PRINT CHR$(27)+"[1m";
1010 PRINT"This is BOLD text"
1020 PRINT CHR$(27)+"[2m";
1030 PRINT"This is DIM text."
1040 PRINT CHR$(27)+"[4m";
1050 PRINT"This text is Underlined."
1060 PRINT CHR$(27)+"[0m";
1070 PRINT"Back to normal"
1080 PRINT CHR$(27)+"[7m";
1090 PRINT"Reversed Text."
1100 PRINT CHR$(27)+"[0m";
1110 WIDTH 80
1120 PRINT"done"
